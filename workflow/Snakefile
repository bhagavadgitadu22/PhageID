import os, re
import glob
import pandas as pd

### Configuration
configfile: os.getcwd()+"/../config/config.yaml"

ENV_DIR = os.getcwd()+"/../envs"
READS_DIR = config["reads_dir"]
RESULTS_DIR = config["results_dir"]

# list of phages
# Get only immediate subdirectories (maxdepth=1)
PHAGES_LIST = [d for d in os.listdir(READS_DIR) 
               if os.path.isdir(os.path.join(READS_DIR, d))]

### Rules to include
# Function to recursively include all .smk files
def include_smk_files(directory):
    for root, _, files in os.walk(directory):
        files.sort()
        for file in files:
            if file.endswith('.smk'):
                include: os.path.join(root, file)

# Include all .smk files from the rules directory and subdirectories
include_smk_files('rules/')

### Global rules
# Assembly rules
rule corrected_assembly_all_phages:
    input: expand(rules.quality_assembly.output, sample = PHAGES_LIST)

# One rule to rule them all
rule pipeline_new_phage:
    output: os.path.join(RESULTS_DIR, "{sample}", "done.touch")
    input: 
        assembly_report = rules.quality_assembly.output,
        pharokka_plot = rules.pharokka_plot.output,
        phold_plot = rules.phold_plot.output,
        genotate_plot = rules.genotate_pharokka_plot.output,
        defenseFinder_report = rules.antiDefenseFinder.output, 
        phageterm_report = rules.phageterm.output,
        vibrant_report = rules.vibrant.output,
        genomad_report = rules.genomad.output,
        checkv_report = rules.checkv.output
    shell: """touch {output}"""

rule pipeline_all_phages:
    input: expand(os.path.join(RESULTS_DIR, "{sample}", "done.touch"), sample = PHAGES_LIST)

# Rule for a whole host
rule pipeline_comparison_phages:
    output: os.path.join(RESULTS_DIR, "combined_viruses", "done.touch")
    input:
	    ani = rules.viruses_dereplicated.output.fna_viruses_derep,
	    pharokka = rules.lovis4u_pharokka.output,
	    genotate = rules.lovis4u_genotate.output
    shell: """touch {output}"""
